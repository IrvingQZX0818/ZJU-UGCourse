.ORIG x3000
MAIN	LD R6, STACK
	JSR INPUTN
	NOT R0, R0
	ADD R0, R0, #1
	ST R0, R
	JSR INPUTN
	NOT R0, R0
	ADD R0, R0, #1
	ST R0, C
	AND R1, R1, #0
	LD R0, TTMAX
	NOT R0, R0
	ADD R0, R0, #1
	ADD R6, R6, R0
	ADD R6, R6, R0
	ADD R3, R6, #0
FORI	LD R0, R
	ADD R0, R1, R0
	BRzp EFORI
	AND R2, R2, #0
FORJ	LD R4, C
	ADD R0, R2, R4
	BRzp EFORJ
	JSR INPUTN
	ADD R5, R3, R2
	STR R0, R5, #0
	LD R0, TTMAX
	ADD R5, R5, R0
	AND R0, R0, #0
	STR R0, R5, #0
	ADD R2, R2, #1
	BRnzp FORJ
EFORJ	LD R4, MAX
	ADD R3, R3, R4
	ADD R1, R1, #1
	BRnzp FORI
EFORI	AND R1, R1, #0
	ADD R5, R6, #0
	AND R4, R4, #0
FORI2	LD R0, R
	ADD R0, R1, R0
	BRzp EFORI2
	AND R2, R2, #0
FORJ2	LD R0, C
	ADD R0, R2, R0
	BRzp EFORJ2
	ADD R3, R5, R2
	JSR DPFUN
	NOT R7, R0
	ADD R7, R7, #1
	ADD R7, R4, R7
	BRzp MSKIP
	ADD R4, R0, #0
MSKIP	ADD R2, R2, #1
	BRnzp FORJ2
EFORJ2	LD R0, MAX
	ADD R5, R5, R0
	ADD R1, R1, #1
	BRnzp FORI2
EFORI2  ADD R1, R4, #0
	JSR OUTPUTN
	TRAP x25
MAX	.FILL #100
TTMAX	.FILL #10000
R	.BLKW 1
C	.BLKW 1
STACK	.FILL xFE00

DPFUN	ADD R6, R6, #-6
	STR R1, R6, #5
	STR R2, R6, #4
	STR R3, R6, #3
	STR R4, R6, #2
	STR R5, R6, #1
	STR R7, R6, #0
	LD R0, TTMAX
	ADD R0, R3, R0
	LDR R4, R0, #0
	BRp DPRET
	LDR R5, R3, #0
	LD R0, DPN100
	ADD R3, R3, R0
	ADD R1, R1, #-1
	BRn DPNEXT1
	JSR DPCMP
	BRnz DPNEXT1	
	JSR DPFUN
	JSR SUBDP
DPNEXT1	LD R0, DP100
	ADD R3, R3, R0
	ADD R3, R3, R0
	ADD R1, R1, #2
	LD R0, R
	ADD R0, R1, R0
	BRzp DPNEXT2
	JSR DPCMP
	BRnz DPNEXT2	
	JSR DPFUN
	JSR SUBDP
DPNEXT2	LD R0, DPN100
	ADD R3, R3, R0
	ADD R1, R1, #-1
	ADD R3, R3, #-1
	ADD R2, R2, #-1
	BRn DPNEXT3
	JSR DPCMP
	BRnz DPNEXT3	
	JSR DPFUN
	JSR SUBDP
DPNEXT3	ADD R3, R3, #2
	ADD R2, R2, #2
	LD R0, C
	ADD R0, R2, R0
	BRzp EDPFUN
	JSR DPCMP
	BRnz EDPFUN
	JSR DPFUN
	JSR SUBDP
EDPFUN	ADD R3, R3, #-1
	ADD R4, R4, #0
	BRp NOSET
	AND R4, R4, #0
NOSET	LD R0, TTMAX
	ADD R3, R3, R0
	ADD R4, R4, #1
	STR R4, R3, #0
DPRET	ADD R0, R4, #0
	LDR R1, R6, #5
	LDR R2, R6, #4
	LDR R3, R6, #3
	LDR R4, R6, #2
	LDR R5, R6, #1
	LDR R7, R6, #0
	ADD R6, R6, #6
	ADD R0, R0, #0
	RET
DPCMP	LDR R0, R3, #0
	NOT R0, R0;
	ADD R0, R0, #1
	ADD R0, R0, R5
	RET
SUBDP	ST R5, DPST5 
	NOT R5, R0;
	ADD R5, R5, #1
	ADD R5, R4, R5
	BRzp SUBRET
	ADD R4, R0, #0
SUBRET	LD R5, DPST5
	RET
DPST5 .BLKW 1
DP100	.FILL #100
DPN100	.FILL #-100


INPUTN	ST R1, IPTNST1
	ST R2, IPTNST2
	ST R3, IPTNST3
	ST R7, IPTNST7
	AND R1, R1, #0
	AND R3, R3, #0
IPTNLO	TRAP x20
	TRAP x21
	LD R2, IPTNSP
	ADD R2, R0, R2
	BRz IPTGSP
	LD R2, IPTNENT
	ADD R2, R0, R2
	BRz IPTGSP
	JSR TIMES10
	LD R2, IPTNMSK
	AND R0, R0, R2 
	ADD R1, R1, R0
	ADD R3, R3, #1
	BRnzp IPTNLO
IPTGSP	ADD R3, R3, #0
	BRz IPTNLO
IPTNEND	ADD R0, R1, #0
	LD R1, IPTNST1
	LD R2, IPTNST2
	LD R3, IPTNST3
	LD R7, IPTNST7
	ADD R0, R0, #0
	RET
IPTNST1	.BLKW 1
IPTNST2	.BLKW 1
IPTNST3 .BLKW 1
IPTNST7	.BLKW 1
IPTNMSK	.FILL x000F
IPTNSP	.FILL x-0020
IPTNENT	.FILL x-000A

; R1 *= 10
TIMES10	ST R2, TM10ST2
	ADD R2, R1, R1	; R2 = R1 << 1
	ADD R1, R2, R2	
	ADD R1, R1, R1	; R1 = R1 << 3
	ADD R1, R2, R1	; R1 = R1 * 10
	LD R2, TM10ST2
	RET
TM10ST2	.BLKW 1

OUTPUTN	ST R7, OPTNST7
	ST R1, OPTNST1
	ST R2, OPTNST2
	ST R3, OPTNST3
	ST R4, OPTNST4
	ST R5, OPTNST5
	ST R6, OPTNST6
	AND R6, R6, #0
	LEA R2, OPTNNUM
OPTNML	LDR R3, R2, #0
	BRz OPTNEND
	AND R0, R0, #0
	AND R4, R4, #0
OPTNCL	ADD R4, R4, R3
	ADD R5, R1, R4
	BRn OPTNCLE
	ADD R0, R0, #1
	BRnzp OPTNCL
OPTNCLE	ADD R5, R6, R0
	BRz OPTNSKP
	LD R5, ASCIIN
	ADD R0, R0, R5
	TRAP x21
	ADD R6, R6, #1
OPTNSKP	NOT R3, R3
	ADD R3, R3, #1
	ADD R4, R4, R3
	ADD R1, R1, R4
	ADD R2, R2, #1
	BRnzp OPTNML
OPTNEND	LD R7, OPTNST7
	LD R1, OPTNST1
	LD R2, OPTNST2
	LD R3, OPTNST3
	LD R4, OPTNST4
	LD R5, OPTNST5
	LD R6, OPTNST6
	RET
OPTNST1	.BLKW 1
OPTNST2	.BLKW 1
OPTNST3	.BLKW 1
OPTNST4	.BLKW 1
OPTNST5	.BLKW 1
OPTNST6	.BLKW 1
OPTNST7	.BLKW 1
OPTNNUM	.FILL x-2710
	.FILL x-03E8
	.FILL x-0064
	.FILL x-000A
	.FILL x-0001
	.FILL x0000
ASCIIN	.FILL x0030
.END